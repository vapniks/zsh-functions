# Widget for browsing command manpages. Requires fzf: https://github.com/junegunn/fzf
# To browse all syscall manpages, at the prompt enter: -s 2 '.*'
() {
    local fzfopts response orig_buffer
    typeset -a response
    zstyle -s ':fzf-apropos:' options fzfopts || fzfopts="--height=100% --preview-window=down:wrap"
    orig_buffer=$BUFFER
    {
	read-from-minibuffer "Apropos command: " "apropos ${NUMERIC+-s ${NUMERIC} }"
	(( $? )) && return
	response=$(eval "${REPLY} 2>&1")
	if [[ $? = 0 ]]; then
	    eval "${REPLY} | fzf ${fzfopts} --preview='man -s {2} {1}' -d '[ ()]' --bind='enter:execute(man -s {2} {1}),alt-v:execute(man -s {2} {1})'"
	else
	    zle -M "fzf-apropos: No matches for ${REPLY}"
	fi
    } always {
	BUFFER=$orig_buffer
    }
}
